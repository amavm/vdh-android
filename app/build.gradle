apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'


android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "app.vdh.org.vdhapp"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0-alpha-10"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {

        def hasMapKeyDebugProperty = project.hasProperty('maps.key.debug')
        def hasMapKeyReleaseProperty = project.hasProperty('maps.key.release')
        def mapKeyEnv = System.getenv('MAP_KEY_CI')

        def mapKeyDebug = hasMapKeyDebugProperty ? project.property('maps.key.debug') : mapKeyEnv
        def mapKeyRelease = hasMapKeyReleaseProperty ? project.property('maps.key.release') : mapKeyEnv

        debug {
            manifestPlaceholders = [maps_api_key: mapKeyDebug]
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [maps_api_key: mapKeyRelease]
        }
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            buildConfigField "boolean", "isMockDataEnabled", "false"
        }
        mock {
            buildConfigField "boolean", "isMockDataEnabled", "true"
        }
        ci {
            // Flavor folder to make CI pass
            buildConfigField "boolean", "isMockDataEnabled", "true"
        }
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation 'com.google.android.material:material:1.0.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.preference:preference:1.1.0"

    implementation "com.google.android.gms:play-services-maps:$gms_version"
    implementation "com.google.android.gms:play-services-places:$gms_version"
    implementation "com.google.android.gms:play-services-location:$gms_location_version"
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Mapbox
    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_version"
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.0.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-places-v9:0.10.0'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.github.esafirm.android-image-picker:imagepicker:1.13.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    //Koin / DI
    implementation 'org.koin:koin-android-viewmodel:1.0.1'

    //Anko
    implementation "org.jetbrains.anko:anko:$anko_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // Ktlint
    ktlint "com.github.shyiko:ktlint:0.31.0"

    implementation "com.airbnb.android:lottie:$lottie_version"

    // Firebase
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta01'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.0'
    implementation 'com.google.firebase:firebase-firestore:21.4.0'

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
